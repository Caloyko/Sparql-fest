@prefix q: <https://bigcat-um.github.io/sparql-examples/Scholia/> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix schema: <https://schema.org/> .
@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix scholia: <http://scholia.toolforge.org/ns/> .
@prefix scholiaAspect: <http://scholia.toolforge.org/ns/aspect/> .
@prefix scholiaView: <http://scholia.toolforge.org/ns/view/> .
@prefix wd: <http://wikidata.org/entity/> .
@prefix wdt: <http://www.wikidata.org/prop/direct/> .
@prefix spex: <https://purl.expasy.org/sparql-examples/ontology#> .
@prefix wikibase: <http://wikiba.se/ontology#> .
@prefix cito: <http://purl.org/spar/cito/> .

q:016 a sh:SPARQLExecutable,
  sh:SPARQLSelectExecutable;
  sh:prefixes _:sparql_examples_prefixes ;
  sh:select '''PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#defaultView:Timeline
PREFIX target: <http://www.wikidata.org/entity/Q97270>

# Timeline for an author
SELECT ?label ?beginning ?ending ?education_degree_label
WITH {
  SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
    ?work wdt:P50 target: .
    ?work wdt:P577 ?publication_date . 
  }
  GROUP BY ?work
} AS %works_with_publication_date
WITH {
  
  SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
    INCLUDE %works_with_publication_date
    OPTIONAL { ?citing_article wdt:P2860 ?work . }
  }
  GROUP BY ?work ?beginning 
} AS %works
WHERE {
  # { Birth target: wdt:P569 ?beginning . BIND("Birth" AS ?label) } UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        INCLUDE %works
      }
      ORDER BY DESC(?number_of_citations)
      LIMIT 1        
    }
    BIND("üìñ publication of most cited article" AS ?label)
  }
  UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        INCLUDE %works
      }
      ORDER BY ?beginning
      LIMIT 1        
    }
    BIND("üìñ first publication" AS ?label)
  }
  UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        INCLUDE %works
      }
      ORDER BY DESC(?beginning)
      LIMIT 1        
    }
    BIND("üìñ latest publication" AS ?label)
  }
  UNION
  {
    # Education
    target: p:P69 ?education_statement .
    ?education_statement ps:P69 ?education .
    ?education rdfs:label ?education_label . 
    FILTER (lang(?education_label) = 'en')
    BIND(CONCAT("ü¶â ", ?education_label) AS ?label)
    OPTIONAL { ?education_statement pq:P580 ?beginning . }
    OPTIONAL { ?education_statement pq:P582 ?ending . }
    OPTIONAL {
      ?education_statement pq:P512 ?education_degree . 
      ?education_degree rdfs:label ?education_degree_label .
      FILTER (lang(?education_degree_label) = 'en')
    }
  }
  UNION
  {
    # Affiliation
    target: p:P1416 | p:P108 ?affiliation_statement .
    ?affiliation_statement ps:P1416 | ps:P108 ?affiliation .
    ?affiliation rdfs:label ?affiliation_label .
    FILTER (lang(?affiliation_label) = 'en')
    BIND(CONCAT("üè† ",?affiliation_label) AS ?label)
    # OPTIONAL { ?affiliation wdt:P18 ?image }
    OPTIONAL { ?affiliation_statement pq:P580 ?beginning . }
    OPTIONAL { ?affiliation_statement pq:P582 ?ending . }
    OPTIONAL {
      ?affiliation_statement pq:P512 ?affiliation_degree . 
      ?affiliation_degree rdfs:label ?affiliation_degree_label .
      FILTER (lang(?affiliation_degree_label) = 'en')
    }
  }
  UNION
  {
    target: p:P166 ?award_statement .
    ?award_statement ps:P166 ?award .
    ?award rdfs:label ?award_label .
    FILTER (lang(?award_label) = 'en')
    BIND(CONCAT("üèÜ ",?award_label) AS ?label)
    ?award_statement pq:P585 ?beginning .
  }
  UNION
  {
    target: wdt:P569 ?beginning .
    BIND(CONCAT("üë∂ born ") AS ?label) .
  }
  UNION
  {
    target: wdt:P570 ?beginning .
    BIND(CONCAT("ü•Ä died") AS ?label) .
  }
			    
} 
''';
  scholia:aspect scholiaAspect:Author ;
  scholia:defaultView scholiaView:Timeline ;
  rdfs:comment "Author aspect: timeline"@en ;
  schema:target <https://query.wikidata.org/sparql> ;
  cito:usesMethodIn <https://github.com/WDscholia/scholia/tree/master/scholia/app/templates/author_timeline.sparql> ;
  dcterms:license <https://www.gnu.org/licenses/gpl-3.0> .
