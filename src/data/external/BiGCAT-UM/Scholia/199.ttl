@prefix q: <https://bigcat-um.github.io/sparql-examples/Scholia/> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix schema: <https://schema.org/> .
@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix scholia: <http://scholia.toolforge.org/ns/> .
@prefix scholiaAspect: <http://scholia.toolforge.org/ns/aspect/> .
@prefix scholiaView: <http://scholia.toolforge.org/ns/view/> .
@prefix wd: <http://wikidata.org/entity/> .
@prefix wdt: <http://www.wikidata.org/prop/direct/> .
@prefix spex: <https://purl.expasy.org/sparql-examples/ontology#> .
@prefix wikibase: <http://wikiba.se/ontology#> .
@prefix cito: <http://purl.org/spar/cito/> .

q:199 a sh:SPARQLExecutable,
  sh:SPARQLSelectExecutable;
  sh:prefixes _:sparql_examples_prefixes ;
  sh:select '''PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX target: <http://www.wikidata.org/entity/Q1769726>

SELECT
  ?count
  
  ?coused ?cousedLabel (CONCAT("/use/", SUBSTR(STR(?coused), 32)) AS ?cousedUrl)
  ("ðŸ”Ž" AS ?zoom)
  (CONCAT("../uses/Q1769726,", SUBSTR(STR(?coused), 32)) AS ?zoomUrl)
  ?cousedDescription
  
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WITH {
  # Find works that are using a specific software,
  # and find other software used in the found works
  SELECT
    ?coused
    (COUNT(DISTINCT ?work) as ?count)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    ?work wdt:P4510 / wdt:P279* target: ;
          wdt:P4510 ?coused .
    FILTER (?coused != target:)
  }
  GROUP BY ?coused
} AS %result
WHERE {
  # Label the result
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }
}
ORDER BY DESC(?count) 
''';
  scholia:aspect scholiaAspect:Use ;
  rdfs:comment "Use aspect: co used"@en ;
  schema:target <https://query.wikidata.org/sparql> ;
  spex:federatesWith <http://wikiba.se/ontology#label> ;
  cito:usesMethodIn <https://github.com/WDscholia/scholia/tree/master/scholia/app/templates/use_co-used.sparql> ;
  dcterms:license <https://www.gnu.org/licenses/gpl-3.0> .
