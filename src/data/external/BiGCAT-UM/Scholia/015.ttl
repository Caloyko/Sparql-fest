@prefix q: <https://bigcat-um.github.io/sparql-examples/Scholia/> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix schema: <https://schema.org/> .
@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix scholia: <http://scholia.toolforge.org/ns/> .
@prefix scholiaAspect: <http://scholia.toolforge.org/ns/aspect/> .
@prefix scholiaView: <http://scholia.toolforge.org/ns/view/> .
@prefix wd: <http://wikidata.org/entity/> .
@prefix wdt: <http://www.wikidata.org/prop/direct/> .
@prefix spex: <https://purl.expasy.org/sparql-examples/ontology#> .
@prefix wikibase: <http://wikiba.se/ontology#> .
@prefix cito: <http://purl.org/spar/cito/> .

q:015 a sh:SPARQLExecutable,
  sh:SPARQLSelectExecutable;
  sh:prefixes _:sparql_examples_prefixes ;
  sh:select '''PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
# Venue statistics for a specific reviewer
PREFIX target: <http://www.wikidata.org/entity/Q97270>

SELECT
  ?count (SAMPLE(?short_name_) AS ?short_name)
  ?venue ?venueLabel
  ?topics ?topicsUrl
WITH {
  SELECT
    (COUNT(DISTINCT ?work) as ?count)
    ?venue
    (GROUP_CONCAT(DISTINCT ?topic_label; separator=", ") AS ?topics)
    (CONCAT("../topics/", GROUP_CONCAT(DISTINCT SUBSTR(STR(?topic), 32); separator=",")) AS ?topicsUrl)
  WHERE {
    {
      ?work wdt:P4032 target: ;
            wdt:P1433 ?venue .
    }
    UNION
    {
      # program committee member for event in a series (work is here the event)
      ?work wdt:P179 ?venue ;
            wdt:P5804 target: 
    }
    UNION
    {
      # program committee member for event not in a series
      ?venue wdt:P5804 target: .
      MINUS { ?venue wdt:P179 [] }
      BIND("dummy" AS ?work)
    }
    OPTIONAL {
      ?venue wdt:P921 ?topic .
      ?topic rdfs:label ?topic_label . FILTER(LANG(?topic_label) = 'en')
    }
  }
  GROUP BY ?venue
} AS %result
WHERE {
  INCLUDE %result
  OPTIONAL { ?venue wdt:P1813 ?short_name_ . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" . }  
} 
GROUP BY ?count ?venue ?venueLabel ?topics ?topicsUrl
ORDER BY DESC(?count)
''';
  scholia:aspect scholiaAspect:Author ;
  rdfs:comment "Author aspect: review statistics"@en ;
  schema:target <https://query.wikidata.org/sparql> ;
  spex:federatesWith <http://wikiba.se/ontology#label> ;
  cito:usesMethodIn <https://github.com/WDscholia/scholia/tree/master/scholia/app/templates/author_review-statistics.sparql> ;
  dcterms:license <https://www.gnu.org/licenses/gpl-3.0> .
